<?xml version="1.0" encoding="utf-8"?>
<s:Application
	xmlns:fx="http://ns.adobe.com/mxml/2009"
	xmlns:s="library://ns.adobe.com/flex/spark"
	xmlns:code="http://code.google.com/p/flexlib/"
	xmlns:view="com.worlize.interactivity.view.*"
	xmlns:local="*" xmlns:mx="library://ns.adobe.com/flex/mx"
	xmlns:view1="com.worlize.view.*"
	xmlns:components="com.worlize.view.components.*"
	xmlns:visualnotification="com.worlize.components.visualnotification.*"
	minWidth="970" minHeight="522"
	skinClass="FlashClientApplicationSkin"
	initialize="handleInitialize(event)"
	applicationComplete="handleApplicationComplete()"
	creationComplete="handleCreationComplete()">
	<s:layout>
		<s:BasicLayout/>
	</s:layout>
	
	<fx:Style source="Styles.css" />
	<fx:Style source="src/com/worlize/view/skins/skins.css" />
 
	<fx:Script>
		<![CDATA[
			import com.adobe.net.URI;
			import com.worlize.components.visualnotification.VisualNotification;
			import com.worlize.control.Marketplace;
			import com.worlize.control.VirtualCurrencyProducts;
			import com.worlize.event.AuthorModeNotification;
			import com.worlize.event.NotificationCenter;
			import com.worlize.event.PreferencesEvent;
			import com.worlize.interactivity.event.InteractivityEvent;
			import com.worlize.interactivity.event.InteractivitySecurityErrorEvent;
			import com.worlize.interactivity.iptscrae.command.CLIENTTYPECommand;
			import com.worlize.interactivity.model.InteractivityUser;
			import com.worlize.interactivity.rpc.InteractivityClient;
			import com.worlize.logging.ArrayLogTarget;
			import com.worlize.model.CurrentUser;
			import com.worlize.model.Directory;
			import com.worlize.model.PreferencesManager;
			import com.worlize.model.WorlizeConfig;
			import com.worlize.model.friends.FriendsList;
			import com.worlize.notification.WorlizeNotification;
			import com.worlize.rpc.HTTPMethod;
			import com.worlize.rpc.WorlizeResultEvent;
			import com.worlize.rpc.WorlizeServiceClient;
			import com.worlize.state.AuthorModeState;
			import com.worlize.view.LockerWindow;
			import com.worlize.view.author.AuthorModePropertiesWindow;
			import com.worlize.view.windows.DirectoryWindow;
			import com.worlize.view.windows.GiftsWindow;
			import com.worlize.view.windows.WorldsWindow;
			
			import flash.desktop.Clipboard;
			import flash.desktop.ClipboardFormats;
			import flash.events.ErrorEvent;
			import flash.net.navigateToURL;
			import flash.sampler.getInvocationCount;
			
			import mx.binding.utils.ChangeWatcher;
			import mx.controls.Alert;
			import mx.core.mx_internal;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.events.PropertyChangeEvent;
			import mx.logging.ILogger;
			import mx.logging.Log;
			import mx.logging.LogEventLevel;
			import mx.logging.targets.TraceTarget;
			import mx.managers.PopUpManager;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.http.HTTPService;
			[Bindable]
			public var client:InteractivityClient = InteractivityClient.getInstance();
			
			[Bindable]
			private var worlizeUser:CurrentUser = CurrentUser.getInstance(); 

			private var preferences:PreferencesManager = PreferencesManager.getInstance();
			
			private var authorPropertiesWindowShowing:Boolean = false;
			private var authorPropertiesWindow:AuthorModePropertiesWindow;
			
			private var manualFriendsListState:String = null;
			
			[Bindable]
			private var logOutToolTip:String;
			
			private var logger:ILogger = Log.getLogger('FlashClient');
			
			private var arrayTarget:ArrayLogTarget;
			
			protected function initLogger():void {
				var traceTarget:TraceTarget = new TraceTarget();
				traceTarget.filters = ['*'];
				traceTarget.level = LogEventLevel.INFO;
				traceTarget.includeDate = false;
				traceTarget.includeTime = false;
				traceTarget.includeCategory = true;
				traceTarget.includeLevel = true;
				Log.addTarget(traceTarget);
				
				arrayTarget = new ArrayLogTarget();
				arrayTarget.maxLogEntries = 300;
				arrayTarget.filters = ['*'];
				arrayTarget.level = LogEventLevel.ALL;
				arrayTarget.includeDate = false;
				arrayTarget.includeTime = true;
				arrayTarget.includeCategory = true;
				arrayTarget.includeLevel = true;
				Log.addTarget(arrayTarget);
			}
			
			protected function handleUncaughtError(event:UncaughtErrorEvent):void
			{
				removeEventListener(UncaughtErrorEvent.UNCAUGHT_ERROR, handleUncaughtError);
				
				if (Capabilities.isDebugger) {
					// Prevent the default error dialog in the debug player
					event.preventDefault();
				}
				
				var logText:String = arrayTarget.getLogText();
				var truncatedLogText:String = "";
				if (logText.length > 61440) { // Only send the last 60KiB of logs
					truncatedLogText = logText.slice(logText.length-61440);
				}
				if (truncatedLogText.length > 0) {
					logText = truncatedLogText;
				}
				
				// Prepare to log the error to the server
				var detail:Object = {
					flash_version: Capabilities.version,
					log_text: logText
				};
				
				if (event.error is Error) {
					var errorObject:Error = event.error;
					detail.error_type = "Error";
					detail.error_id = errorObject.errorID;
					detail.stack_trace = errorObject.getStackTrace();
					if (errorObject.message && "toString" in errorObject.message) {
						detail.message = errorObject.message.toString();
					}
					if (errorObject.name && "toString" in errorObject.name) {
						detail.name = errorObject.name.toString();
					}
				}
				else if (event.error is ErrorEvent) {
					var errorEvent:ErrorEvent = event.error;
					detail.error_type = "ErrorEvent";
					detail.error_id = errorEvent.errorID;
					detail.name = errorEvent.type;
					if (detail.message && "toString" in detail.message) {
						detail.message = errorEvent.toString();						
					}
				}
				else {
					detail.type = "Object";
					if ('toString' in event.error) {
						detail.message = event.error.toString();
					}
					else {
						detail.message = "Object does not implement toString()";
					}
				}
				
				var serviceClient:WorlizeServiceClient = new WorlizeServiceClient();
				serviceClient.addEventListener(FaultEvent.FAULT, function(event:FaultEvent):void {
					// If there is an error reporting the error we can't do much..
				});
				serviceClient.addEventListener(WorlizeResultEvent.RESULT, function(event:WorlizeResultEvent):void {
					// There's no result to handle.
				});
				serviceClient.send('/client_errors.json', HTTPMethod.POST, detail);
				
				Alert.show("Uhoh!  Something went wrong.  An error report has been sent to our engineers, and we'll try to get it taken care of as soon as possible!",
						   "Error", Alert.OK, null, function(event:CloseEvent):void {
							   ExternalInterface.call('redirectToHomepage');
						   });
			}
			
			private function handleApplicationComplete():void {
				// Set Frame Rate to 30fps
				stage.frameRate = 30;
				
				// Test global event handling
//				setTimeout(function():void {
//					throw new Error("Foo Bar Error");
//					var errorEvent:ErrorEvent = new ErrorEvent("myGreatErrorEvent", false, false, "Error Event Text", 25);
//					dispatchEvent(errorEvent);
//				}, 5000);
				logger.info("Application URL: " + url);
				logger.info("Flash Player Version: " + Capabilities.version);
				
				stage.addEventListener(KeyboardEvent.KEY_DOWN, handleStageKeyDown);
				stage.addEventListener(Event.RESIZE, handleStageResize);
				updateFriendsListAutoDisplay();
				
				if (ExternalInterface.call('getFacebookLoginStatus')) {
					logOutToolTip = "Log out of Worlize and Facebook";
				}
				else {
					logOutToolTip = "Log out of Worlize";
				}
				
				if (preferences.initialized) {
					handlePreferencesInitialized();
				}
				else {
					preferences.addEventListener(PreferencesEvent.PREFERENCES_INITIALIZED, handlePreferencesInitialized);
				}
				
//				analytics.tracker.setDomainName(".worlize.com");
//				analytics.tracker.trackPageview("/enter");
			}
			
			private function handlePreferencesInitialized(event:PreferencesEvent=null):void {
				if (preferences.preferenceDefined('launchDirectoryOnLoad')) {
					if (preferences.getPreference('launchDirectoryOnLoad')) {
						DirectoryWindow.open(systemManager.getSandboxRoot());
					}
				}
				else {
					DirectoryWindow.open(systemManager.getSandboxRoot());
				}
			}
			
			private function handleStageResize(event:Event):void {
				updateFriendsListAutoDisplay();
			}
			
			private function handleStageKeyDown(event:KeyboardEvent):void {
				if (event.keyCode == Keyboard.NUMPAD_ADD) {
					client.setFace(client.currentUser.face+1);
				}
				else if (event.keyCode == Keyboard.NUMPAD_SUBTRACT) {
					client.setFace(client.currentUser.face-1);
				}
			}

			private function handleCreationComplete():void {
				initLogger();
				
				ExternalInterface.call('hideLoadingOverlay');
				
				client.addEventListener(InteractivityEvent.CONNECT_COMPLETE, handleConnectComplete);
				client.addEventListener(InteractivityEvent.CONNECT_FAILED, handleConnectFailed);
				client.addEventListener(InteractivityEvent.CONNECT_START, handleConnectStart);
				client.addEventListener(InteractivityEvent.GOTO_URL, handleGotoURL);
					
				client.setCyborg("");
				
				client.connect();
				
				authorPropertiesWindow = new AuthorModePropertiesWindow();
				authorPropertiesWindow.x = this.width - authorPropertiesWindow.width - 15;
				authorPropertiesWindow.y = 75;

				NotificationCenter.addListener(AuthorModeNotification.AUTHOR_DISABLED, handleAuthorDisabled);
				NotificationCenter.addListener(AuthorModeNotification.SELECTED_ITEM_CHANGED, handleAuthorSelectedItemChanged);
			}
			
			private var authorMode:Boolean = false;
			
			private function handleAuthorDisabled(notification:AuthorModeNotification):void {
				hideAuthorPropertiesWindow();
			}
			
			private function handleAuthorSelectedItemChanged(notification:AuthorModeNotification):void {
				if (!authorPropertiesWindowShowing && notification.newValue !== null) {
					showAuthorPropertiesWindow();
				}
				else {
					if (notification.newValue === null) {
						hideAuthorPropertiesWindow();
					}
				}
			}
			
			private function showAuthorPropertiesWindow():void {
				if (authorPropertiesWindow) {
					PopUpManager.addPopUp(authorPropertiesWindow, systemManager.getSandboxRoot());
					authorPropertiesWindowShowing = true;
				}
			}
			private function hideAuthorPropertiesWindow():void {
				if (authorPropertiesWindow && authorPropertiesWindowShowing) {
					PopUpManager.removePopUp(authorPropertiesWindow);
				}
				authorPropertiesWindowShowing = false;
			}
			
			
			private function handleGotoURL(event:InteractivityEvent):void {
				var url:String = event.url;
				Alert.show("Do you want to visit this url in your browser?\n\n" + event.url,
						   "External Link",
						   Alert.YES | Alert.NO,
						   null,
						   function(event:CloseEvent):void {
							  if (event.detail == Alert.YES) {
								  var req:URLRequest = new URLRequest(url);
								  navigateToURL(req, "_BLANK");
							  }
						   });
			}
			
			private function handleConnectComplete(event:InteractivityEvent):void {
				if (AuthorModeState.getInstance().enabled) {
					chatWindow.currentState = "authormode";
				}
				else {
					chatWindow.currentState = "connected";
				}
			}
			
			private function handleConnectFailed(event:InteractivityEvent):void {
				Alert.show(event.text, "Error");
				chatWindow.currentState = "disconnected"
			}
			
			private function handleConnectStart(event:InteractivityEvent):void {
				chatWindow.currentState = "connecting";
			}
			
			private function disconnect():void {
				client.disconnect();
			}
			
			private function handleGiftsClick():void {
				var point:Point = new Point(giftsButton.x, giftsButton.y);
				var globalPoint:Point = topButtonBar.localToGlobal(point);
				GiftsWindow.open(systemManager.getSandboxRoot(), globalPoint.x, globalPoint.y + giftsButton.height);
			}
			
			private function handleDirectoryClick():void {
				DirectoryWindow.toggleOpen(systemManager.getSandboxRoot());
			}
			
			private function showComingSoon(message:String):void {
				var notification:VisualNotification = new VisualNotification(message, "Hang tight!");
				notification.show();
			}
			
			private function openMarketplace():void {
				if (Marketplace.marketplaceEnabled) {
					Marketplace.open();
				}
				else {
					showComingSoon("You will soon be able to buy premium backgrounds, avatars, and objects in the Marketplace!");
				}
			}
			
			private function showVirtualCurrencyProducts():void {
				VirtualCurrencyProducts.show();
			}
			
			private function handleAvatarsClick():void {
				LockerWindow.toggleOpen(systemManager.getSandboxRoot());
			}
			
			private function verifySufficientRoomForFriendsList():Boolean {
				return (stage.width > chatWindow.x + chatWindow.width + friendsListPanel.width);
			}
			
			private function handleFriendsButtonChange(event:Event):void {
				if (ToggleButton(event.target).selected) {
					showFriendsList();
					if (verifySufficientRoomForFriendsList()) {
						manualFriendsListState = null;
					}
					else {
						manualFriendsListState = "open";
					}
				}
				else {
					hideFriendsList();
					manualFriendsListState = "closed";
				}
			}
			
			private function updateFriendsListAutoDisplay():void {
				if (manualFriendsListState === null) {
					if (verifySufficientRoomForFriendsList()) {
						if (!friendsListPanel.visible) {
							showFriendsList();
						}
					}
					else {
						if (friendsListPanel.visible) {
							hideFriendsList();
						}
					}					
				}
			}
			
			private function showFriendsList():void {
				friendsListPanel.visible = true;
				friendsListToggleButton.selected = true;
			}
			
			private function hideFriendsList():void {
				friendsListPanel.visible = false;
				friendsListToggleButton.selected = false;
			}
			
			protected function handleLogoutClick(event:MouseEvent):void
			{
				var message:String = "Are you sure you want to log out of Worlize?";
				if (ExternalInterface.call('getFacebookLoginStatus') as Boolean) {
					message = "Are you sure you want to log out of Worlize and Facebook?";
				}
				Alert.show(message,
						   "Logout",
						   Alert.YES | Alert.NO,
						   null,
						   function(event:CloseEvent):void {
							   if (event.detail == Alert.YES) {
								   ExternalInterface.call("logout");
							   }
						   },
						   null,
						   Alert.NO);
			}
			
			private function handleForumsClick(event:MouseEvent):void {
				var request:URLRequest = new URLRequest("https://forums.worlize.com/");
				navigateToURL(request, "_blank");
			}
			
			private function handleHomeClick():void {
				client.gotoRoom('home');
				// FIXME: The currentUser.worldEntrance value will be wrong if
				// the user has deleted the first room in their world.
//				var homeEntrance:String = WorlizeConfig.getInstance().currentUser.worldEntrance;
//				if (homeEntrance) {
//					if (client.currentRoom.id === homeEntrance) {
//						var notification:VisualNotification = new VisualNotification(
//							"You are already at the entrance to your home world!",
//							"Home"
//						);
//						notification.show();
//					}
//					else {
//						client.gotoRoom('home');
//					}
//				}
			}
			
			public function get baseURI():String {
				var appURI:URI = new URI(url);
				return appURI.scheme + "://" + appURI.authority;
			}
			
			private function getUserJoinLink():String {
				return baseURI + "/users/" + encodeURIComponent(client.worlizeConfig.currentUser.username) + "/join";
			}
			
			private function focusChatBox():void {
				var notification:WorlizeNotification = new WorlizeNotification(WorlizeNotification.FOCUS_CHAT_BOX_NOTIFICATION);
				NotificationCenter.postNotification(notification);
			}
			
			protected function handleInitialize(event:FlexEvent):void {
				addEventListener(UncaughtErrorEvent.UNCAUGHT_ERROR, handleUncaughtError);
			}
			
		]]>
	</fx:Script>

	
	<view1:ChatWindow id="chatWindow" client="{client}" top="40" left="10" />	

	<s:HGroup top="3" left="10" right="10" gap="15" verticalAlign="middle">
		
		<s:BorderContainer
			id="topButtonBar"
			tabChildren="false"
			height="25"
			borderVisible="false"
			backgroundColor="#FFFFFF" cornerRadius="12">
			<s:layout>
				<s:HorizontalLayout gap="20"
									paddingTop="1"
									paddingLeft="3"
									paddingRight="-1" />
			</s:layout>
			
			<mx:LinkButton
				styleName="topNavButton"
				color="#0C9dea"
				icon="@Embed(source='src/assets/icons/icon_myworlz.png')"
				click="handleDirectoryClick()"
				toolTip="Browse a list of active rooms"
				label="DIRECTORY" />
			<mx:LinkButton
				styleName="topNavButton"
				color="#479d13"
				icon="@Embed(source='src/assets/icons/icon_home.png')"
				click="handleHomeClick()"
				toolTip="Go to your home world"
				label="HOME" />
			<mx:LinkButton
				styleName="topNavButton"
				color="#F15601"
				icon="@Embed(source='src/assets/icons/icon_avatar.png')"
				click="handleAvatarsClick()"
				toolTip="Change your Avatar"
				label="AVATARS" />
			<mx:LinkButton
				id="giftsButton"
				styleName="topNavButton"
				color="#9e1F63"
				icon="@Embed(source='src/assets/icons/icon_gifts.png')"
				toolTip="See if you have received any gifts"
				click="handleGiftsClick()"
				label="GIFTS" />
		</s:BorderContainer>
		
		<s:HGroup
			verticalAlign="middle"
			tabChildren="false"
			gap="12">
			
			<components:CapacityButton
				line1="Coins: {worlizeUser.coins}"
				line2="Bucks: {worlizeUser.bucks}"
				toolTip="Click to add coins or bucks"
				click="showVirtualCurrencyProducts();"
				/>
			
			<components:SocialShareWidget
				  click="focusChatBox()"
				  tabChildren="false"
				  description="a link to your location"
				  linkFunction="{getUserJoinLink}"
				  twitterText="I'm chatting right now in Worlize, a 2d virtual world platform.  Come join me!"
				  twitterRelated="worlize"
				  twitterVia="worlize"
				  facebookName="Come chat with me LIVE right now in Worlize!"
				  facebookCaption="Worlize: Your World, Realized"
				  facebookDescription="I'm online right now in Worlize, a stylish avatar chat hangout where you can be whatever you want.  Come join me!"
				  currentRoom="{client.currentRoom}"
				  currentWorld="{client.currentWorld}"
				  paddingLeft="10" />
			
			<!--
			<mx:LinkButton
				styleName="topNavButton"
				color="#B58B0B"
				icon="@Embed(source='src/assets/icons/icon_marketplace.png')"
				click="openMarketplace()"
				label="Marketplace" />
			-->
		</s:HGroup>
	</s:HGroup>
	
	<s:HGroup right="0" top="0" height="40" gap="0" verticalAlign="middle">
		<mx:LinkButton label="FORUMS"
					   height="100%"
					   styleName="topNavButtonRight forumsButton"
					   color="#333333"
					   toolTip="Submit feature requests or get help from the community in the forums."
					   click="handleForumsClick(event)"/>
		<mx:LinkButton label="LOG OUT"
					   height="100%"
					   styleName="topNavButtonRight logOutButton"
					   color="#333333"
					   toolTip="{logOutToolTip}"
					   click="handleLogoutClick(event)"/>
		<s:ToggleButton label="FRIENDS"
						id="friendsListToggleButton"
						height="100%"
						icon="@Embed(source='src/assets/icons/icon_friends.png')"
						styleName="friendsListToggleButton"
						toolTip="Find and invite your friends"
						change="handleFriendsButtonChange(event)" />
	</s:HGroup>
	
	<view1:FriendsListPanel id="friendsListPanel" visible="false"
							friendsList="{FriendsList.getInstance()}"
							top="40" right="0" bottom="0" width="250" />
	
	<visualnotification:VisualNotifier id="visualNotifier" top="20" left="0" right="0" bottom="0" />
	
</s:Application>
