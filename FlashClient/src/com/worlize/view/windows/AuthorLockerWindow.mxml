<?xml version="1.0" encoding="utf-8"?>
<components:WorlizeWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
						  xmlns:s="library://ns.adobe.com/flex/spark" 
						  xmlns:mx="library://ns.adobe.com/flex/mx"
						  xmlns:components="com.worlize.view.components.*"
						  title="My Locker"
						  titleBarColor="#962626" titleTextColor="#962626" 
						  windowIcon="@Embed('assets/icons/large/w_icon_backgrounds.png')"
						  minWidth="485" minHeight="385"
						  creationComplete="handleCreationComplete()"
						  width="485" height="422" xmlns:author="com.worlize.view.author.*">
	
	<fx:Script>
		<![CDATA[
			import com.worlize.event.AuthorModeNotification;
			import com.worlize.event.NotificationCenter;
			import com.worlize.model.PreferencesManager;
			
			import flex.utils.spark.resize.ResizeManager;
			
			import mx.events.CloseEvent;
			import mx.events.IndexChangedEvent;
			import mx.events.ResizeEvent;
			import mx.managers.PopUpManager;
			
			import spark.events.TitleWindowBoundsEvent;
			private static var _open:Boolean = false;
			private static var _instance:AuthorLockerWindow;
			
			public static function toggleOpen(parent:DisplayObject):void {
				if (_open) {
					close();
				}
				else {
					open(parent);
				}
			}
			
			public static function open(parent:DisplayObject):void {
				var preferences:PreferencesManager = PreferencesManager.getInstance();
				if (_open) {
					PopUpManager.bringToFront(_instance);
					_instance.setFocus();
				}
				else {
					var needsPosition:Boolean = false;
					if (!_instance) {
						needsPosition = true;
						_instance = new AuthorLockerWindow();
						_instance.addEventListener(CloseEvent.CLOSE, function(event:CloseEvent):void {
							close();
						});
						_instance.addEventListener(ResizeManager.RESIZE_END, function(event:ResizeEvent):void {
							preferences.setPreference('authorLockerSize', [_instance.width, _instance.height]);
						});
						_instance.addEventListener(TitleWindowBoundsEvent.WINDOW_MOVE_END, function(event:TitleWindowBoundsEvent):void {
							preferences.setPreference('authorLockerPosition', [_instance.x, _instance.y]);
						});
					}
					PopUpManager.addPopUp(_instance, parent);
					if (needsPosition) {
						var coordinates:Array = preferences.getPreference('authorLockerPosition') as Array;
						var size:Array = preferences.getPreference('authorLockerSize') as Array;
						var selectedTabIndex:int = preferences.getPreference('authorLockerSelectedTab');
						if (coordinates && size &&
							_instance.boundsAreValid(coordinates[0], coordinates[1], size[0], size[1]))
						{
							_instance.x = coordinates[0];
							_instance.y = coordinates[1];
							_instance.width = size[0];
							_instance.height = size[1];
							if (!(isNaN(selectedTabIndex) || _instance.lockerViewStack.numChildren < selectedTabIndex)) {
								_instance.lockerViewStack.selectedIndex = selectedTabIndex;
							}
						}
						else {
							PopUpManager.centerPopUp(_instance);
						}
					}
					_open = true;
				}
			}
			
			public static function close():void {
				if (_open && _instance) {
					PopUpManager.removePopUp(_instance);
					_open = false;
				}
			}
			
			private function handleCreationComplete():void {
				NotificationCenter.addListener(AuthorModeNotification.AUTHOR_DISABLED, handleAuthorModeDisabled);
			}
			
			private function handleAuthorModeDisabled(notification:AuthorModeNotification):void {
				close();
			}
			
			private function handleViewStackChange(event:IndexChangedEvent):void {
				var preferences:PreferencesManager = PreferencesManager.getInstance();
				preferences.setPreference("authorLockerSelectedTab", event.newIndex);
			}
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<components:controlBarContent>
		<!--
		<mx:LinkButton
			styleName="topNavButton"
			color="#B58B0B"
			icon="@Embed(source='src/assets/icons/icon_marketplace.png')"
			label="Apps Marketplace" />
		-->
	</components:controlBarContent>
	
	<s:VGroup gap="0" paddingTop="10" width="100%" height="100%">
		<s:Group width="100%">
			<s:TabBar left="10" id="tabBar"
					  dataProvider="{lockerViewStack}" />
			<s:Rect width="100%" height="1" bottom="0">
				<s:fill>
					<s:SolidColor color="#777777" />
				</s:fill>
			</s:Rect>
		</s:Group>
		
		<mx:ViewStack id="lockerViewStack" width="100%" height="100%" change="handleViewStackChange(event)">
			<s:NavigatorContent id="backgroundsTab" label="Backgrounds">
				<author:MyBackgroundsLockerView width="100%" height="100%" />
			</s:NavigatorContent>
			
			<s:NavigatorContent id="objectsTab" label="Objects">
				<author:MyObjectsLockerView width="100%" height="100%" />
			</s:NavigatorContent>
			
			<s:NavigatorContent id="appsTab" label="Apps">
				<author:MyAppsLockerView width="100%" height="100%" />
			</s:NavigatorContent>
		</mx:ViewStack>
	</s:VGroup>
	
	
	
</components:WorlizeWindow>
