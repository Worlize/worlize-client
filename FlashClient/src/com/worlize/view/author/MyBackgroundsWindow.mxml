<?xml version="1.0" encoding="utf-8"?>
<components:WorlizeWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
						  xmlns:s="library://ns.adobe.com/flex/spark" 
						  xmlns:mx="library://ns.adobe.com/flex/mx"
						  xmlns:components="com.worlize.view.components.*"
						  title="My Backgrounds"
						  titleBarColor="#962626" titleTextColor="#962626" 
						  windowIcon="@Embed('assets/icons/large/w_icon_backgrounds.png')"
						  minWidth="525" minHeight="415"
						  creationComplete="handleCreationComplete()"
						  width="525" height="415">
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import com.worlize.control.BackgroundUploader;
			import com.worlize.control.Uploader;
			import com.worlize.interactivity.rpc.InteractivityClient;
			import com.worlize.model.BackgroundImageAsset;
			import com.worlize.model.BackgroundImageInstance;
			import com.worlize.model.PreferencesManager;
			import com.worlize.model.locker.BackgroundsLocker;
			import com.worlize.rpc.HTTPMethod;
			import com.worlize.rpc.WorlizeResultEvent;
			import com.worlize.rpc.WorlizeServiceClient;
			import com.worlize.view.itemrenderers.BackgroundLockerItemRenderer;
			
			import flex.utils.spark.resize.ResizeManager;
			
			import mx.binding.utils.ChangeWatcher;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.ListEvent;
			import mx.events.MoveEvent;
			import mx.events.ResizeEvent;
			import mx.graphics.SolidColor;
			import mx.managers.PopUpManager;
			import mx.managers.SystemManager;
			import mx.rpc.events.FaultEvent;
			
			import spark.events.IndexChangeEvent;
			import spark.events.TitleWindowBoundsEvent;
			import spark.primitives.Rect;
			private static var _open:Boolean = false;
			private static var _instance:WorlizeWindow;
			
			[Bindable]
			private var uploader:BackgroundUploader = new BackgroundUploader();
			
			[Bindable]
			private var backgroundsLocker:BackgroundsLocker = new BackgroundsLocker();
			
			public static function toggleOpen(parent:DisplayObject):void {
				if (_open) {
					close();
				}
				else {
					open(parent);
				}
			}
			
			public static function open(parent:DisplayObject):void {
				var preferences:PreferencesManager = PreferencesManager.getInstance();
				if (_open) {
					PopUpManager.bringToFront(_instance);
					_instance.setFocus();
				}
				else {
					var needsPosition:Boolean = false;
					if (!_instance) {
						needsPosition = true;
						_instance = new MyBackgroundsWindow();
						_instance.addEventListener(CloseEvent.CLOSE, function(event:CloseEvent):void {
							close();
						});
						_instance.addEventListener(ResizeManager.RESIZE_END, function(event:ResizeEvent):void {
							preferences.setPreference('myBackgroundsSize', [_instance.width, _instance.height]);
						});
						_instance.addEventListener(TitleWindowBoundsEvent.WINDOW_MOVE_END, function(event:TitleWindowBoundsEvent):void {
							preferences.setPreference('myBackgroundsPosition', [_instance.x, _instance.y]);
						});
					}
					PopUpManager.addPopUp(_instance, parent);
					if (needsPosition) {
						var coordinates:Array = preferences.getPreference('myBackgroundsPosition') as Array;
						var size:Array = preferences.getPreference('myBackgroundsSize') as Array;
						if (coordinates && size &&
							_instance.boundsAreValid(coordinates[0], coordinates[1], size[0], size[1]))
						{
							_instance.x = coordinates[0];
							_instance.y = coordinates[1];
							_instance.width = size[0];
							_instance.height = size[1];
						}
						else {
							PopUpManager.centerPopUp(_instance);
						}
					}
					_open = true;
				}
			}
			
			public static function close():void {
				if (_open && _instance) {
					PopUpManager.removePopUp(_instance);
					_open = false;
				}
			}
			
			private function handleCreationComplete():void {
				backgroundsLocker.load();
			}
			
			private function uploadBackground():void {
				if (backgroundsLocker.emptySpaces > 0) {
					uploader.browse();
				}
				else {
					Alert.show("You don't have enough empty spaces.  Either delete some existing backgrounds or purchase more space.", "Insufficient Space"); 
				}
			}
			
			private function handleBackgroundChange(event:IndexChangeEvent):void {
				var backgroundInstance:BackgroundImageInstance = backgroundImagesList.selectedItem;
				var roomId:String = InteractivityClient.getInstance().currentRoom.id;
				
				var client:WorlizeServiceClient = new WorlizeServiceClient();
				client.addEventListener(WorlizeResultEvent.RESULT, function(event:WorlizeResultEvent):void {
					if (event.resultJSON.success) {
						backgroundsLocker.updateItems(event.resultJSON.data.updated_background_instances);
						backgroundImagesList.selectedIndex = -1;
					}
					else {
						Alert.show("Unable to set background: " + event.resultJSON.description);
					}
				});
				client.addEventListener(FaultEvent.FAULT, function(event:FaultEvent):void {
					Alert.show("Unable to set room background.");
				});
				client.send("/rooms/" + roomId + "/set_background.json", HTTPMethod.POST, {
					background_instance_guid: backgroundInstance.guid
				});
			}
		]]>
	</fx:Script>
	
	
	
	<components:controlBarContent>
		<components:CapacityButton
			line1="Capacity: {backgroundsLocker.capacity}"
			line2="Current: {backgroundsLocker.count}"
			toolTip="Click to add more locker capacity"/>
	</components:controlBarContent>

	<s:HGroup right="10" top="5" gap="10" verticalAlign="middle">
		<components:UploadProgressIndicator
			uploader="{uploader}" />

		<mx:LinkButton
			id="uploadButton"
			click="uploadBackground()"
			enabled="{uploader.state == Uploader.STATE_READY}"
			styleName="lockerNavButton"
			color="#2b9610"
			icon="@Embed(source='src/assets/icons/icon_upload.png')"
			label="Upload" />
	</s:HGroup>
	
	<s:List top="40" right="10" left="10" bottom="10"
			id="backgroundImagesList" dataProvider="{backgroundsLocker.backgroundInstances}"
			borderVisible="false"
			change="handleBackgroundChange(event)"
			itemRenderer="com.worlize.view.itemrenderers.BackgroundLockerItemRenderer">
		<s:layout>
			<s:TileLayout verticalGap="10" horizontalGap="10" />
		</s:layout>
	</s:List>
	
	
	
</components:WorlizeWindow>
