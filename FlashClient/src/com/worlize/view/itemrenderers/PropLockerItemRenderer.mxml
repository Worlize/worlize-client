<?xml version="1.0" encoding="utf-8"?>
<s:ItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx"
				width="82" height="82"
				mouseDown="handleMouseDown(event)"
				
				xmlns:components="com.worlize.view.components.*">
	
	<fx:Script>
		<![CDATA[
			import com.worlize.interactivity.rpc.InteractivityClient;
			import com.worlize.model.Prop;
			import com.worlize.model.PropInstance;
			import com.worlize.model.gifts.Gift;
			import com.worlize.view.windows.SendGiftWindow;
			
			import flash.net.navigateToURL;
			
			import mx.binding.utils.ChangeWatcher;
			import mx.controls.Alert;
			import mx.core.DragSource;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.managers.DragManager;
			
			/**
			 *  @private
			 *  Storage for the data property.
			 */
			private var _data:Object;
			
			private var emptySlotWatcher:ChangeWatcher;
			
			[Bindable("dataChange")]
			override public function get data():Object
			{
				return _data;
			}
			
			/**
			 *  @private
			 */
			override public function set data(value:Object):void
			{
				if (_data === value) { return; }
				if (_data) {
					// remove old change watchers
					removeWatchers();
				}
				_data = value;
				if (_data) {
					// add change watchers
					addWatchers(); 
				}
				handleDataStateChanged();
				dispatchEvent(new FlexEvent(FlexEvent.DATA_CHANGE));
			}
			
			private function handleDataStateChanged(event:Event = null):void {
				if (_data === null) {
					this.enabled = false;
				}
				else {
					if (_data.emptySlot) {
						this.enabled = false;
					}
					else {
						this.enabled = true;
					}
				}
			}
			
			private function removeWatchers():void {
				emptySlotWatcher.unwatch();
			}
			
			private function addWatchers():void {
				emptySlotWatcher = ChangeWatcher.watch(_data, 'emptySlot', handleDataStateChanged);
			}
			
			private function requestPropDelete():void {
				PropInstance(data).requestDelete();
			}
			
			private function handleDeleteClick(event:MouseEvent):void {
				event.stopImmediatePropagation();
				var alert:Alert = Alert.show(
					"Are you sure you want to delete this prop?",
					"Delete Prop",
					Alert.YES | Alert.NO,
					null,
					handleAlertClose,
					null,
					Alert.NO);
			}
			
			private function handleAlertClose(event:CloseEvent):void {
				if (event.detail == Alert.YES) {
					requestPropDelete();
				}
			}
			
			private function stopPropagation(event:MouseEvent):void {
				event.stopImmediatePropagation();
				event.preventDefault();
			}
			
			private function handleGiftClick(event:MouseEvent):void {
				event.stopImmediatePropagation();
				
				var prop:Prop = PropInstance(data).prop;
				SendGiftWindow.launch(systemManager.getSandboxRoot(), prop);
			}
			
			private function handleEditClick(event:MouseEvent):void {
				if (data.editable) {
//					var urlRequest:URLRequest = new URLRequest(PropInstance(data).aviaryEditUrl);
//					navigateToURL(urlRequest, '_aviary');
				}
			}
			
			private function nullFunction():void { }
			
			protected function handleMouseDown(event:MouseEvent):void {
				var root:DisplayObject = systemManager.getSandboxRoot();
				root.addEventListener(MouseEvent.MOUSE_UP, handleStageMouseUp);
				root.addEventListener(MouseEvent.MOUSE_MOVE, handleStageMouseMove);
			}
			
			protected function handleStageMouseUp(event:MouseEvent):void {
				var root:DisplayObject = systemManager.getSandboxRoot();
				root.removeEventListener(MouseEvent.MOUSE_UP, handleStageMouseUp);
				root.removeEventListener(MouseEvent.MOUSE_MOVE, handleStageMouseMove);
			}
			
			protected function handleStageMouseMove(event:MouseEvent):void {
				var root:DisplayObject = systemManager.getSandboxRoot();
				root.removeEventListener(MouseEvent.MOUSE_UP, handleStageMouseUp);
				root.removeEventListener(MouseEvent.MOUSE_MOVE, handleStageMouseMove);
				
				if (event.buttonDown) {
					var dragSource:DragSource = new DragSource();
					dragSource.addData({
						startMouseX: event.stageX,
						startMouseY: event.stageY,
						guid: data.prop.guid
					}, 'propFromLocker');
					DragManager.doDrag(this, dragSource, event);
				}
			}
			
			
			
		]]>
	</fx:Script>
	
	<s:states>
		<s:State name="normal" />
		<s:State name="hovered" stateGroups="active" />
		<s:State name="selected" stateGroups="active" />
	</s:states>
	
	
	<s:Rect left="0" top="0" right="0" bottom="0">
		<s:stroke>
			<s:SolidColorStroke weight="1" color="#CCCCCC" />
		</s:stroke>
		<s:fill>
			<s:SolidColor color="#F0F0F0" />
		</s:fill>
	</s:Rect>
	
	<components:PropThumbnailView left="1" top="1" dataProvider="{data}" />
	
	<s:Label visible="{data.emptySlot}" horizontalCenter="0" verticalCenter="0" text="Empty" />
	
	<s:Rect left="0" top="0" right="0" bottom="0" includeIn="active">
		<s:stroke>
			<s:SolidColorStroke joints="miter"
								weight.hovered="1" color.hovered="#ffe49b" alpha.hovered="0.3"
								color="#f5b501" weight="5" />
		</s:stroke>
		<s:fill>
			<s:SolidColor color="#ffe49b" alpha="0.3" alpha.selected="0.4" />
		</s:fill>
	</s:Rect>
	
	<!--
	<mx:Image source="@Embed(source='src/assets/icons/icon_authormode_red.png')"
			  toolTip="Edit with Aviary Image Editor" useHandCursor="true"
			  includeIn="active" bottom="4" right="56"
			  alpha="{data.editable ? 1.0 : 0.0}"
			  mouseDown="{data.editable ? stopPropagation(event) : nullFunction()}"
			  click="handleEditClick(event)" />
	-->
	
	<mx:Image source="@Embed(source='../../../../assets/icons/icon_gifts.png')"
			  toolTip="Send as Gift" useHandCursor="true"
			  includeIn="active" bottom="4" right="30"
			  mouseDown="stopPropagation(event)"
			  click="handleGiftClick(event)" />
	
	<mx:Image source="@Embed(source='../../../../assets/icons/icon_delete.png')"
			  toolTip="Delete" useHandCursor="true"
			  includeIn="active" bottom="4" right="4" mouseDown="stopPropagation(event)" click="handleDeleteClick(event)" />
	
</s:ItemRenderer>
