<?xml version="1.0" encoding="utf-8"?>
<s:ItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx"
				xmlns:friends="com.worlize.view.itemrenderers.friends.*"
				autoDrawBackground="false"
				dataChange="handleDataChange(event)"
				initialize="handleInitialize(event)"
				width="100%"
				currentState="normal">
	
	<s:states>
		<s:State name="down" />
		<s:State name="selected" />
		<s:State name="hovered" />
		<s:State name="normal" />
	</s:states>
	
	<fx:Script>
		<![CDATA[
			import com.worlize.interactivity.rpc.InteractivityClient;
			import com.worlize.model.friends.FriendsList;
			import com.worlize.model.friends.FriendsListEntry;
			
			import flash.net.navigateToURL;
			
			import mx.binding.utils.ChangeWatcher;
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.controls.Menu;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.events.MenuEvent;
			
			[Bindable]
			private var client:InteractivityClient = InteractivityClient.getInstance();
			
			[Bindable]
			private var buttonLabel:String;
			
			private var onlineWatcher:ChangeWatcher;
			
			[Bindable]
			private var menu:Menu;
			
			private var stateUpdatesSuspended:Boolean = false;
			
			
			private function handleInitialize(event:Event):void {
				onlineWatcher = ChangeWatcher.watch(this, ['data','online'], handleOnlineStatusChange);
			}
			
			private function handleDataChange(event:FlexEvent):void {
				if (data === null) { return; }
				stateUpdatesSuspended = false;
				updateMenu();
				updateOnlineStatus();
			}
			
			override protected function getCurrentRendererState():String {
				if (stateUpdatesSuspended) {
					return currentState;
				}
				return super.getCurrentRendererState();
			}
			
			private function updateOnlineStatus():void {
				if (data === null) { return; }
				if (data is FriendsListEntry && (data as FriendsListEntry).online) {
					buttonLabel = "Goto";
				}
				else {
					buttonLabel = "Visit Home";
				}
			}
			
			private function handleOnlineStatusChange(event:Event):void {
				updateOnlineStatus();
			}
			
			private function visitWorlz():void {
				client.gotoRoom((data as FriendsListEntry).worldEntrance);
			}
			
			private function updateMenu():void {
				if (menu) {
					menu.removeEventListener(MenuEvent.ITEM_CLICK, handleMenuItemClick);
				}	
				var entry:FriendsListEntry = data as FriendsListEntry;
				var menuData:Array = [];
				if (entry.online) {
					menuData.push({ type: 'normal', label: 'Go to Current Location', action: 'join' });
					menuData.push({ type: 'normal', label: 'Invite to Your Location', action: 'invite' });
				}
				menuData.push({ type: 'normal', label: 'Visit Home', action: 'visitWorlz' });
				if (entry.facebookProfile) {
					menuData.push({ type: 'normal', label: 'View Facebook Profile', action: 'facebookProfile' });
				}
				if (entry.twitterProfile) {
					menuData.push({ type: 'normal', label: 'View Twitter Profile', action: 'twitterProfile' });
				}
				menuData.push({ type: 'normal', label: 'Remove Friend', action: 'unfriend' });
				
				menu = Menu.createMenu(null, menuData, true);
				menu.addEventListener(MenuEvent.ITEM_CLICK, handleMenuItemClick);
			}
			
			private function handleMenuOpen(event:Event):void {
				stateUpdatesSuspended = true;
			}
			
			private function handleMenuClose(event:Event):void {
				stateUpdatesSuspended = false;
				invalidateRendererState();
			}
			
			private function handleMenuItemClick(event:MenuEvent):void {
				if (!event.item || !event.item.action) { return; }
				switch (event.item.action) {
					case "join":
						visitFriend();
						break;
					case "visitWorlz":
						visitWorlz();
						break;
					case "twitterProfile":
						viewTwitterProfile();
						break;
					case "facebookProfile":
						viewFacebookProfile();
						break;
					case "invite":
						inviteFriend();
						break;
					case "requestToJoin":
						requestToJoin();
						break;
					case "unfriend":
						unfriend();
						break;
					default:
						break;
				}
			}
			
			private function handleActionButtonClick(event:MouseEvent):void {
				if ((data as FriendsListEntry).online) {
					visitFriend();
				}
				else {
					visitWorlz();
				}
			}
			
			private function stopMouseEventPropagation(event:MouseEvent):void {
				event.stopImmediatePropagation();
			}
			
			private function visitFriend():void {
				(data as FriendsListEntry).gotoLocation();
			}
			
			private function viewTwitterProfile():void {
				var r:URLRequest = new URLRequest((data as FriendsListEntry).twitterProfile);
				navigateToURL(r, '_blank');
			}
			
			private function viewFacebookProfile():void {
				var r:URLRequest = new URLRequest((data as FriendsListEntry).facebookProfile);
				navigateToURL(r, '_blank');
			}
			
			private function inviteFriend():void {
				(data as FriendsListEntry).invite();
			}
			
			private function requestToJoin():void {
				(data as FriendsListEntry).requestToJoin();
			}
			
			private function unfriend():void {
				var entry:FriendsListEntry = data as FriendsListEntry;
				var message:String;
				var name:String
				if (entry.friendType === FriendsListEntry.TYPE_FACEBOOK && entry.autoSynced) {
					name = entry.name;
					message = name + " is from your Facebook friends list and was automatically added.\n\n" +
						      "Are you sure you want to unfriend them?";
				}
				else {
					name = entry.username;
					message = "Are you sure you want to remove " + name + " from your friends list?"; 
				}
				Alert.show(message,
						   "Unfriend " + name,
						   Alert.YES | Alert.NO,
						   null,
						   function(event:CloseEvent):void {
							   if (event.detail == Alert.YES) {
								   entry.unfriend();
							   }
						   }
				);
			}
		]]>
	</fx:Script>
	<s:Rect left="0" right="0" bottom="0" top="0" includeIn="hovered,down,selected">
		<s:fill>
			<s:SolidColor color="#DAEEF7"
						  color.down="#BFE5F6" />	
		</s:fill>
	</s:Rect>
	
	<s:HGroup width="100%" top="0" bottom="0" paddingRight="3" verticalAlign="middle">

		<friends:FriendItemRenderer data="{data}" width="100%" />
		
		<mx:PopUpButton id="actionButton"
						excludeFrom="normal"
						label="{buttonLabel}"
						popUp="{menu}"
						mouseDown="stopMouseEventPropagation(event)"
						mouseUp="stopMouseEventPropagation(event)"
						open="handleMenuOpen(event)"
						close="handleMenuClose(event)"
						click="handleActionButtonClick(event)" />
	</s:HGroup>
	
				
	
</s:ItemRenderer>
