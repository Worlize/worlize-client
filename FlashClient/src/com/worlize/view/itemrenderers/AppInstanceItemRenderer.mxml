<?xml version="1.0" encoding="utf-8"?>
<s:ItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx"
				width="100%"
				autoDrawBackground="false">
	<s:layout>
		<s:HorizontalLayout gap="3" verticalAlign="baseline"
							paddingTop="0" paddingLeft="6"
							paddingBottom="0" paddingRight="3" />
	</s:layout>
	
	<fx:Script>
		<![CDATA[
			import com.worlize.model.AppInstance;
			
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			
			/**
			 *  @private
			 *  Storage for the data property.
			 */
			[Bindable]
			private var _data:AppInstance;
			
			[Bindable("dataChange")]
			override public function get data():Object
			{
				return _data;
			}
			
			/**
			 *  @private
			 */
			override public function set data(value:Object):void
			{
				if (_data === value) { return; }
				if (value is AppInstance) {
					_data = AppInstance(value);
					invalidateRendererState();
					dispatchEvent(new FlexEvent(FlexEvent.DATA_CHANGE));
				}
			}
			
			protected function requestDelete():void {
				Alert.show("Are you sure you want to delete this app instance?",
						   "Confirm", Alert.YES | Alert.NO, null,
						   function(event:CloseEvent):void {
							   if (event.detail === Alert.YES) {
								   _data.requestDelete();
							   }
						   });
			}
			
			protected function requestRemoveFromRoom():void {
				Alert.show("Are you sure you want to remove this app from the room \"" + _data.room.name + "\"?",
					"Confirm", Alert.YES | Alert.NO, null,
					function(event:CloseEvent):void {
						if (event.detail === Alert.YES) {
							_data.requestRemoveFromRoom();
						}
					});
			}
			
		]]>
	</fx:Script>
	
	<s:Label text="{_data.room ? 'Used in: ' + _data.room.name : '(Not in use)'}" maxDisplayedLines="1"
			 width="100%" paddingRight="3" />
	<s:Button label="Remove from Room" enabled="{Boolean(_data.room)}"
			  click="requestRemoveFromRoom()" />
	<s:Button label="Delete" click="requestDelete()" />
	
</s:ItemRenderer>
