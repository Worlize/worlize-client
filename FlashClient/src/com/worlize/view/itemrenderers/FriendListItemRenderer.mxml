<?xml version="1.0" encoding="utf-8"?>
<s:ItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx"
				height="47" width="100%" autoDrawBackground="false">
	
	<fx:Script>
		<![CDATA[
			import com.worlize.interactivity.rpc.InteractivityClient;
			import com.worlize.model.FriendsList;
			import com.worlize.model.FriendsListEntry;
			import com.worlize.view.windows.FriendsWindow;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.controls.Menu;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.events.MenuEvent;
			
			[Bindable]
			private var client:InteractivityClient = InteractivityClient.getInstance();
			
			[Bindable]
			private var _data:FriendsListEntry;
			
			[Bindable(event='dataChange')]
			override public function set data(newValue:Object):void {
				if (_data !== newValue) {
					if (newValue is FriendsListEntry) {
						_data = FriendsListEntry(newValue);
						updateMenu();
					}
					else {
						_data = null;
					}
					dispatchEvent(new FlexEvent('dataChange'));
				}
			}
			override public function get data():Object {
				return _data;
			}
			
			private function visitFriend(data:FriendsListEntry):void {
				client.gotoRoom(data.worldEntrance);
				FriendsWindow.close();
			}
			
			private var menu:Menu;
			
			private function updateMenu():void {
				if (menu) {
					menu.removeEventListener(MenuEvent.ITEM_CLICK, handleMenuItemClick);
				}	
				var menuData:Array = [
					{ type: 'normal', label: 'Invite ' + _data.username + ' to come here.', action: 'invite' },
					{ type: 'normal', label: 'Request to join ' + _data.username + '.', action: 'requestToJoin' },
					{ type: 'separator' },
					{ type: 'normal', label: 'Unfriend', action: 'unfriend' }
				];
				menu = Menu.createMenu(null, menuData, true);
				menu.addEventListener(MenuEvent.ITEM_CLICK, handleMenuItemClick);
				popUpButton.popUp = menu;
			}
			
			private function handleMenuItemClick(event:MenuEvent):void {
				if (!event.item || !event.item.action) { return; }
				switch (event.item.action) {
					case "invite":
						break;
					case "requestToJoin":
						break;
					case "unfriend":
						unfriend();
						break;
					default:
						break;
				}
			}
			
			private function unfriend():void {
				FriendsWindow.lockOpen();
				Alert.show("Are you sure you want to remove " + _data.username + " from your friends list?",
						   "Unfriend " + _data.username,
						   Alert.YES | Alert.NO,
						   null,
						   function(event:CloseEvent):void {
							   if (event.detail == Alert.YES) {
								   _data.unfriend();
							   }
							   FriendsWindow.releaseLock();
						   }
				);
			}
		]]>
	</fx:Script>
		
	
	<s:Rect width="42" height="42"
			top="2" left="2">
		<s:stroke>
			<s:SolidColorStroke color="#666666" weight="1" joints="miter" caps="square" />
		</s:stroke>
		<s:fill>
			<s:SolidColor color="#F0F0F0" />
		</s:fill>
	</s:Rect>
	
	<mx:Image width="40" height="40" top="3" left="3" 
			  source="@Embed(source='src/assets/icons/unknown_user.png')" />
	
	<mx:Image id="onlineDot" left="48" verticalCenter="0" width="13" height="13"
			  visible="{_data.online}"
			  source="@Embed(source='src/assets/icons/dot-online.png')" />
	
	<mx:Image id="offlineDot" left="48" verticalCenter="0" width="13" height="13"
			  visible="{!_data.online}"
			  source="@Embed(source='src/assets/icons/dot-offline.png')" />
	
	
	<s:HGroup left="64" right="3" verticalCenter="0" verticalAlign="middle">
		<s:Label fontFamily="Arial" width="100%"
				 text="{_data.username}" />

		<mx:PopUpButton id="popUpButton" label="Visit Worlz"
						click="visitFriend(_data)" />
				
	</s:HGroup>
	
				
	
</s:ItemRenderer>
