<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 x="75" y="75"
		 width="561" height="300"
		 addedToStage="handleAddedToStage()"
		 >
	
	<fx:Script>
		<![CDATA[
			import com.worlize.event.TutorialEvent;
			import com.worlize.model.tutorial.TutorialData;
			import com.worlize.model.tutorial.TutorialSlideDefinition;
			
			[Bindable]
			public var tutorialData:TutorialData;
			
			private var currentSlide:int = 0;
			
			private function handleAddedToStage():void {
				showSlide(0);
			}
			
			private function showSlide(slideNumber:int):void {
				var slide:TutorialSlideDefinition = tutorialData.slides[slideNumber];
				currentSlide = slideNumber;
				
				var loader:Loader = new Loader();
				var urlRequest:URLRequest = new URLRequest(slide.imageURL);
				loader.contentLoaderInfo.addEventListener(Event.COMPLETE, function(event:Event):void {
					currentImage.source = Bitmap(loader.content);
				});
				loader.contentLoaderInfo.addEventListener(IOErrorEvent.IO_ERROR, function(event:IOErrorEvent):void {
					
				});
				loader.load(urlRequest);
				
				if (currentSlide == tutorialData.slides.length - 1) {
					nextButton.enabled = false;
					nextButton.alpha = 0.7;
				}
				else {
					nextButton.enabled = true;
					nextButton.alpha = 1.0;
					var nextSlide:TutorialSlideDefinition = tutorialData.slides[slideNumber+1];
					nextImage.source = nextSlide.imageURL;
				}
				
				if (currentSlide == 0) {
					prevButton.enabled = false;
					prevButton.alpha = 0.7;
				}
				else {
					prevButton.enabled = true;
					prevButton.alpha = 1.0;
				}
			}
			
			private function nextSlide():void {
				if (currentSlide+1 < tutorialData.slides.length) {
					showSlide(++currentSlide);
				}
			}
			
			private function prevSlide():void {
				if (currentSlide > 0) {
					showSlide(--currentSlide);
				}
			}
			
			private function handleSkipClick():void {
				tutorialData.skipTutorial = true;
				tutorialData.closeTutorial();
			}
			
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>

	<s:Group id="imageGroup">
		<mx:Image id="nextImage" alpha="0.0" />
		<mx:Image id="currentImage" />
	</s:Group>
	
	<s:HGroup id="buttonGroup" right="0" bottom="0">
		<mx:Image id="skipButton"
				  buttonMode="true" useHandCursor="true"
				  click="handleSkipClick()"
				  source="@Embed(source='src/assets/tutorial/SkipButton.png')" />
		<mx:Image id="prevButton"
				  buttonMode="true" useHandCursor="true"
				  click="prevSlide()"
				  source="@Embed(source='src/assets/tutorial/PreviousButton.png')" />
		<mx:Image id="nextButton"
				  buttonMode="true" useHandCursor="true"
				  click="nextSlide()"
				  source="@Embed(source='src/assets/tutorial/NextButton.png')" />
	</s:HGroup>
	
</s:Group>
